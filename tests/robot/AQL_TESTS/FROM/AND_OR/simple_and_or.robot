*** Settings ***
Documentation   CHECK AQL RESPONSE ON QUERIES WITH SIMPLE 'AND' and 'OR'
...             - Covers: https://github.com/ehrbase/AQL_Test_CASES/blob/feature/CDR-998_CONTAINS_and/or/FROM_TEST_SUIT.MD#simple-and-and-or
...         - *Precondition:* 1. Create OPT; 2. Create EHR; 3. Create Composition
...         - *Postcondition:* Delete EHR using ADMIN endpoint. This is deleting compositions linked to EHR.
Resource        ../../../_resources/keywords/aql_keywords.robot
Library     DataDriver
...     file=${PROJECT_ROOT}/tests/robot/_resources/test_data_sets/aql/fields_and_results/from/combinations/from_simple_and_or.csv
...     dialect=excel

#Suite Setup  Skip    enable Setup 'Precondition' if AQL checks are passing !!!
#Suite Teardown  Skip    enable Teardown 'Admin Delete EHR For AQL' if AQL checks are passing !!!
Suite Setup     Precondition        #enable this keyword if AQL checks are passing !!!
Suite Teardown  Admin Delete EHR For AQL       #enable this keyword if AQL checks are passing !!!


*** Test Cases ***
${statement}
    [Tags]      not-ready   to-be-updated-after-doc-change-cdr-998
    [Template]      Execute Query
    ${statement}     ${expected_file}    ${nr_of_results}


*** Keywords ***
Precondition
    Upload OPT For AQL      aql-conformance-ehrbase.org.v0.opt
    Create EHR For AQL
    Commit Composition For AQL      aql-conformance-ehrbase.org.v0_contains.json

Execute Query
    [Arguments]     ${statement}     ${expected_file}    ${nr_of_results}
    ${query}    Set Variable    ${statement}
    Set AQL And Execute Ad Hoc Query    ${query}
    Log     ${expected_file}
    ${expected_result}      Set Variable    ${EXPECTED_JSON_DATA_SETS}/from/${expected_file}
    ##${exclude_paths}    Create List    root['rows'][0][0]['uid']
    Length Should Be    ${resp_body['rows']}     ${nr_of_results}
    ${diff}     compare json-string with json-file
    ...     ${resp_body_actual}     ${expected_result}
    ...     ignore_order=${TRUE}    ignore_string_case=${TRUE}
    #exclude_paths=${exclude_paths}
    Should Be Empty    ${diff}    msg=DIFF DETECTED!